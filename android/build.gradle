apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "org.ligi.survivalmanual"

        minSdkVersion 14
        targetSdkVersion 26
        versionCode 41
        versionName "4.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "SurvivalManual-$versionName"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions  "fire", "store"

    productFlavors {
        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }

        withFirebase {
            dimension "fire"
        }

        noFirebase {
            dimension "fire"
        }


    }

    android.variantFilter { variant ->
        def firebase = variant.getFlavors().get(0).name
        def store = variant.getFlavors().get(1).name

        if ((project.hasProperty("singleFlavor") && (store != 'forPlay'))
                || ((store == 'forAmazon' || store == 'forPlay') && firebase == 'noFirebase')
                || (store == 'forFDroid' && firebase != 'noFirebase')) {
            variant.setIgnore(true)
        }
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    // ugly but the best workaround I found for getting the permission for spoon screenshot writing to SD-Card
    // http://stackoverflow.com/questions/25276537/how-do-i-change-android-permission-settings-when-running-tests
    sourceSets {
        debug {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        warning 'MissingTranslation'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

}

configurations {

    // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestImplementation.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestImplementation.exclude group: 'com.android.support', module: 'design'
    androidTestImplementation.exclude group: 'com.android.support', module: 'support-v4'
    androidTestImplementation.exclude group: 'com.android.support', module: 'support-annotations'
    androidTestImplementation.exclude group: 'com.android.support', module: 'preference-v7'
    androidTestImplementation.exclude module: 'recyclerview-v7'
    androidTestImplementation.exclude module: 'kotlin-stdlib'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:preference-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"

    implementation "com.chibatching.kotpref:kotpref:2.1.2"
    implementation "com.chibatching.kotpref:initializer:2.1.2"

    implementation 'com.github.ligi:intellij-markdown:android_via_jitpack'

    implementation 'com.github.ligi:TouchImageView:2.1'
    implementation "com.github.ligi:ExtraCompats:0.4"
    implementation 'com.github.ligi:tracedroid:1.4'

    implementation 'com.github.ligi:KAXT:0.17'

    implementation 'com.github.hotchemi:permissionsdispatcher:2.3.2'

    kapt 'com.github.hotchemi:permissionsdispatcher-processor:2.3.2'

    forPlayImplementation 'com.github.ligi.snackengage:snackengage-playrate:0.13'
    forFDroidImplementation 'com.github.ligi.snackengage:snackengage-playrate:0.13'
    forAmazonImplementation 'com.github.ligi.snackengage:snackengage-amazonrate:0.13'

    androidTestImplementation "com.android.support:support-annotations:$support_version"
    androidTestImplementation "com.android.support:appcompat-v7:$support_version"
    androidTestImplementation "com.android.support:design:$support_version"
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'

    androidTestImplementation 'com.github.ligi:trulesk:0.19'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.8.9'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation "com.android.support:support-annotations:$support_version"

    withFirebaseImplementation "com.google.firebase:firebase-core:$firebase_version"
    withFirebaseImplementation "com.google.firebase:firebase-crash:$firebase_version"
}

if (hasProperty("isCI")) {
    apply plugin: 'com.google.gms.google-services'
}

spoon {
    debug = true
    grantAllPermissions = true
}

